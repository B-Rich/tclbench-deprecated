proc canvas-create {w} {
    canvas $w
    destroy $w
}

proc draw {cw {reps 100}} {
    $cw delete all
    if {$::tcl_version > 8.2} {
	for {set i 0} {$i < $reps} {incr i 10} {
	    $cw create rect [list 20 $i 23 [expr {$i+3}]] -fill red
	    $cw create line [list 28 $i 35 $i] -fill yellow -arrow last \
		    -arrowshape {3 6 3}
	    $cw create text [list 40 $i] -text num_$i -fill black -anchor w
	}
    } else {
	for {set i 0} {$i < $reps} {incr i 10} {
	    $cw create rect 20 $i 23 [expr {$i+3}] -fill red
	    $cw create line 28 $i 35 $i -fill yellow -arrow last \
		    -arrowshape {3 6 3}
	    $cw create text 40 $i -text num_$i -fill black -anchor w
	}
    }
}

eval destroy [winfo children .]

bench -iters 500 -desc "CANVAS create" \
	-body {canvas-create .c}

canvas .one

bench -iters 500 -desc "CANVAS create (one exists)" \
	-body {canvas-create .c}

bench -iters 500 -desc "CANVAS configure -bg" \
	-pre {destroy .c ; canvas .c} \
	-body {.c config -bg black} \
	-post {destroy .c}

bench -iters 500 -desc "CANVAS cget/incr -width" \
	-pre {destroy .c ; canvas .c} \
	-body {set width [.c cget -width] ; incr width} \
	-post {destroy .c}

bench -iters 500 -desc "CANVAS configure all" \
	-pre {destroy .c ; canvas .c} \
	-body {
    .c configure -background black \
	    -borderwidth 4 \
	    -closeenough 5.0 \
	    -confine yes \
	    -cursor arrow \
	    -height 100 \
	    -highlightbackground yellow \
	    -highlightcolor pink \
	    -highlightthickness 3 \
	    -insertbackground red \
	    -insertborderwidth 2 \
	    -insertofftime 300 \
	    -insertontime 200 \
	    -insertwidth 4 \
	    -relief raised \
	    -scrollregion {0 0 1000 1000} \
	    -selectbackground blue \
	    -selectborderwidth 2 \
	    -selectforeground white \
	    -takefocus "of course" \
	    -width 200 \
	    -xscrollcommand [list .sx set] \
	    -xscrollincrement 20 \
	    -yscrollcommand [list .sy set] \
	    -yscrollincrement 20 \
	} \
	-post {destroy .c}

bench -iters 400 -desc "CANVAS simple draw 100" \
	-pre {destroy .c; canvas .c} \
	-body {draw .c 100} \
	-post {destroy .c}

bench -iters 200 -desc "CANVAS simple draw 1000" \
	-pre {destroy .c; canvas .c} \
	-body {draw .c 1000} \
	-post {destroy .c}

eval destroy [winfo children .]
